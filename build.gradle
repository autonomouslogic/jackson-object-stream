plugins {
    id "java"
    id "idea"
    id "maven"
    id "signing"
}

group "com.autonomouslogic.jacksonobjectstream"

def getVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine './bin/version.sh'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersion()

sourceCompatibility = 1.8

ext {
    jacksonVersion = "2.9.9"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testImplementation "junit:junit:4.12"
    testImplementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    testImplementation "com.google.guava:guava:28.0-jre"
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Jackson Object Stream"
                packaging "jar"
                description "Simple streaming of JSON-encoded objects."
                url "https://bitbucket.org/autonomouslogic/jackson-object-stream"

                scm {
                    connection "scm:hg:https://bitbucket.org/autonomouslogic/jackson-object-stream"
                    developerConnection "scm:hg:https://bitbucket.org/autonomouslogic/jackson-object-stream"
                    url "https://bitbucket.org/autonomouslogic/jackson-object-stream"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "kennethjor"
                        name "Kenneth Jorgensen"
                        email "kenneth@autonomouslogic.com"
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = "5.6.2"
    distributionType = "ALL"
}
